lines: ("\n"? line "\n")+
line: "' " NUMBER " "~3..6 NUMBER " " TYPE " : " data
data: sheet_info | cell_value | cell_formula | string_value
sheet_info: "Sheet Information - " STRING
cell_value: "Cell Value, " STRING
string_value: "String Value of a " STRING

cell_formula: "Cell Formula - " cell " len=" NUMBER (" " stack_item)+ " "?
cell: "R" NUMBER "C" NUMBER
stack_item: stack_int | stack_func | stack_concat
            | stack_cell_ref | stack_formula | stack_str
            | stack_bool | stack_attr | stack_add
stack_int: "ptgInt " NUMBER
stack_func: "ptgFuncV " NAME " (" HEX_NUMBER ")"
stack_concat: "ptgConcat"
stack_cell_ref: "ptgRef" "V"? " " cell
stack_formula: "ptgFuncVarV args " NUMBER " func " NAME " (" HEX_NUMBER ")"
stack_str: "ptgStr \"" NON_QUOTE_STRING "\""
stack_bool: "ptgBool " BOOLEAN
stack_attr: "ptgAttr"
stack_add: "ptgAdd"

NUMBER: ("0".."9")+
HEX_NUMBER: "0x" ("0".."9"|"a".."f")+
TYPE: "BOUNDSHEET" | "LABEL" | "FORMULA" | "STRING"
STRING: /[^\n]+/
NON_QUOTE_STRING: /[^"]+/
NAME: /[\w\.]+/
BOOLEAN: "TRUE" | "FALSE"
